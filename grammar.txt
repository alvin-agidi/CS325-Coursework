program ::= extern_list decl decl_list

extern_list ::= extern extern_list | epsilon
extern ::= "extern" type_spec IDENT "(" params ")" ";"

decl_list ::= decl decl_list | epsilon
decl ::= "void" IDENT func_body | var_type IDENT decl_body
decl_body :: = ";" | func_body

var_type ::= "int" | "float" | "bool"

type_spec ::= "void" | var_type

func_body ::= "(" params ")" block

params ::= param param_list | "void" | epsilon
param_list ::= "," param param_list | epsilon
param ::= var_type IDENT

block ::= "{" local_decl_list stmt_list "}" 

local_decl_list ::= local_decl local_decl_list | epsilon
local_decl ::= var_type IDENT ";"

stmt_list ::= stmt stmt_list | epsilon
stmt ::= expr_stmt 
    | block
    | if_stmt 
    | while_stmt 
    | return_stmt

expr_stmt ::= expr ";" | ";"

while_stmt ::= "while" "(" expr ")" stmt

if_stmt ::= "if" "(" expr ")" block else_stmt

else_stmt  ::= "else" block | epsilon

return_stmt ::= "return" expr_stmt

expr ::= IDENT "=" expr | rval_1

# Operators in order of increasing precedence
rval_1 ::= rval_2 rval_1_list
rval_1_list ::= "||" rval_1 | epsilon
rval_2 ::= rval_3 rval_2_list
rval_2_list ::= "&&" rval_2 | epsilon
rval_3 ::= rval_4 rval_3_list
rval_3_list ::= "==" rval_3 | "!=" rval_3 | epsilon
rval_4 ::= rval_5 rval_4_list
rval_4_list ::= "<" rval_4 | "<=" rval_4 | ">" rval_4 | ">=" rval_4 | epsilon
rval_5 ::= rval_6 rval_5_list
rval_5_list ::= "+" rval_5 | "-" rval_5 | epsilon
rval_6 ::= rval_term rval_6_list
rval_6_list ::= "*" rval_6 | "/" rval_6 | "%" rval_6 | epsilon
rval_term ::= "-" rval_term
    | "!" rval_term
    | "(" expr ")"
    | IDENT ident_body
    | INT_LIT | FLOAT_LIT | BOOL_LIT

ident_body ::= "(" args ")" | epsilon

args ::= expr arg_list | epsilon
arg_list ::= "," expr arg_list | epsilon 

# FIRST SETS

FIRST(program) = FIRST(extern_list) + FIRST(decl) = {"extern", epsilon, "void", "int", "float", "bool"}

FIRST(extern_list) = FIRST(extern) + {epsilon} = {"extern", epsilon}

FIRST(decl) = {"void"} + FIRST(var_decl) = {"void", "int", "float", "bool"}

FIRST(decl_list) = FIRST(decl) + {epsilon} = {"void", "int", "float", "bool", epsilon}

FIRST(params) = FIRST(param) + {"void", epsilon} = {"int", "float", "bool", "void", epsilon}

FIRST(param) = FIRST(var_type) = {"int", "float", "bool"}

FIRST(type_spec) = {"void"} + FIRST(var_decl) = {"void", "int", "float", "bool"}

FIRST(func_body) = {"("}

FIRST(block) = {"{"}

FIRST(local_decl_list) = FIRST(local_decl) + {epsilon} = {epsilon, "int", "float", "bool"}

FIRST(local_decl) = FIRST(var_type) = {"int", "float", "bool"}

FIRST(stmt_list) = FIRST(stmt) + {epsilon} = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "{", "if", "while", "return", epsilon}

FIRST(stmt) = FIRST(expr_stmt) + FIRST(if_stmt) + FIRST(while_stmt) + FIRST(return_stmt) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "{", "if", "while", "return"}

FIRST(expr_stmt) = FIRST(expr) + {";"} = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";"}

FIRST(expr) = {IDENT} + FIRST(rval_1) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(while_stmt) = {"while"}

FIRST(if_stmt) = {"if"}

FIRST(else_stmt) = {"else"}

FIRST(return_stmt) = {"return"}

FIRST(rval_1) = FIRST(rval_2) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(rval_1_list) = {"||", epsilon}

FIRST(rval_2) = FIRST(rval_3) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(rval_2_list) = {"&&", epsilon}

FIRST(rval_3) = FIRST(rval_4) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(rval_3_list) = {"==", "!=", epsilon}

FIRST(rval_4) = FIRST(rval_5) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(rval_4_list) = {"<", "<=", ">", ">=", epsilon}

FIRST(rval_5) = FIRST(rval_6) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(rval_5_list) = {"+", "-", epsilon}

FIRST(rval_6) = FIRST(rval_term) = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(rval_6_list) = {"*", "/", "%", epsilon}

FIRST(rval_term) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT}

FIRST(args) = FIRST(expr) + {epsilon} = {"-", "!", "(", IDENT, INT_LIT, FLOAT_LIT, BOOL_LIT, epsilon}

FIRST(args_list) = {",", epsilon}

# FOLLOW SETS

(ALL first sets exclude epsilon)

FOLLOW(program) = {}

FOLLOW(extern_list) = FIRST(decl) = {"void", "int", "float", "bool"}

FOLLOW(decl) = FIRST(decl_list) + FOLLOW(program) = {"void", "int", "float", "bool"}

FOLLOW(decl_list) = FOLLOW(program) = {}

FOLLOW(params) = {")"}

FOLLOW(block) = FOLLOW(stmt) + FOLLOW(func_body) + FIRST(else_stmt) + FOLLOW(if_stmt) + FOLLOW(else_stmt) = {"void", "int", "float", "bool", "else", IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(func_body) = FOLLOW(decl_body) + FOLLOW(decl) = {"void", "int", "float", "bool"}

FOLLOW(decl_body) = FOLLOW(decl) = {"void", "int", "float", "bool"}

FOLLOW(stmt_list) = {"}"}

FOLLOW(stmt) = FIRST(stmt_list) + FOLLOW(stmt_list) + FOLLOW(while_list) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(expr_stmt) = FOLLOW(stmt) + FOLLOW(return_stmt) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(if_stmt) = FOLLOW(stmt) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(else_stmt) = FOLLOW(if_stmt) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(while_stmt) = FOLLOW(stmt) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(return_stmt) = FOLLOW(stmt) = {IDENT, "-", "!", "(", INT_LIT, FLOAT_LIT, BOOL_LIT, ";", "if", "while", "return", "}"}

FOLLOW(expr) = {")", ";"} + FIRST(arg_list) + FOLLOW(args) + FOLLOW(arg_list) = {")", ";", ","}

FOLLOW(args) = {")"}

FOLLOW(arg_list) = FOLLOW(args) = {")"}

FOLLOW(rval_term) = FIRST(rval_6_list) + FOLLOW(rval_6_list) + FOLLOW(rval_6) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_6_list) = FOLLOW(rval_6) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_6) = FIRST(rval_5_list) + FOLLOW(rval_5_list) + FOLLOW(rval_5) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_5_list) = FOLLOW(rval_5) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_5) = FIRST(rval_4_list) + FOLLOW(rval_4_list) + FOLLOW(rval_4) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_4_list) = FOLLOW(rval_4) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_4) = FIRST(rval_3_list) + FOLLOW(rval_3_list) + FOLLOW(rval_3) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_3_list) = FOLLOW(rval_3) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_3) = FIRST(rval_2_list) + FOLLOW(rval_2_list) + FOLLOW(rval_2) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_2_list) = FOLLOW(rval_2) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_2) = FIRST(rval_1_list) + FOLLOW(rval_1_list) + FOLLOW(rval_1) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_1_list) = FOLLOW(rval_1) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}

FOLLOW(rval_1) = FOLLOW(expr) = {"*", "/", "%", "+", "-", "<", "<=", ">", ">=", "==", "!=", "&&", "||", ")", ";", ","}
